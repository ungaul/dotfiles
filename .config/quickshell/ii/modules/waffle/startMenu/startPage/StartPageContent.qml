pragma ComponentBehavior: Bound
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Quickshell
import Quickshell.Hyprland
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.widgets
import qs.modules.waffle.looks

WPanelPageColumn {
    id: root

    WPanelSeparator {}

    StartPageApps {
        Layout.fillHeight: true
    }

    WPanelSeparator {}

    StartFooter {
        Layout.fillWidth: true
    }

    component StartFooter: FooterRectangle {
        implicitHeight: 63

        StartUserButton {
            anchors {
                left: parent.left
                leftMargin: 52
                bottom: parent.bottom
                bottomMargin: 12
            }
        }

        Row {
            anchors {
                right: parent.right
                rightMargin: 52
                bottom: parent.bottom
                bottomMargin: 12
            }
            spacing: 4

            SettingsButton {}
            FamilySwitchButton {}
            ReloadButton {}
            PowerButton {}
        }
    }

    property string settingsQmlPath: Quickshell.shellPath("settings.qml")

    component SettingsButton: WBorderlessButton {
        implicitWidth: 40
        implicitHeight: 40

        contentItem: Item {
            FluentIcon {
                anchors.centerIn: parent
                icon: "settings"
                implicitSize: 20
            }
        }

        WToolTip {
            text: qsTr("Settings")
        }

        onClicked: {
            GlobalStates.searchOpen = false;
            Quickshell.execDetached(["bash", "-c", `XDG_CURRENT_DESKTOP=gnome ~/.config/hypr/hyprland/scripts/launch_first_available.sh 'qs -p ${root.settingsQmlPath}' 'systemsettings'`]);
        }
    }

    component FamilySwitchButton: WBorderlessButton {
        implicitWidth: 40
        implicitHeight: 40

        contentItem: Item {
            FluentIcon {
                anchors.centerIn: parent
                icon: "arrow-sync"
                implicitSize: 20
            }
        }

        WToolTip {
            text: qsTr("Switch to ii")
        }

        onClicked: {
            GlobalStates.searchOpen = false;
            Config.options.panelFamily = "ii";
        }
    }

    component ReloadButton: WBorderlessButton {
        implicitWidth: 40
        implicitHeight: 40

        contentItem: Item {
            FluentIcon {
                anchors.centerIn: parent
                icon: "arrow-clockwise"
                implicitSize: 20
            }
        }

        WToolTip {
            text: qsTr("Reload Hyprland & Quickshell")
        }

        onClicked: {
            Hyprland.dispatch("hl.dsp.config.reload()");
            Quickshell.reload(true);
        }
    }

    component PowerButton: WBorderlessButton {
        id: powerButton
        implicitWidth: 40
        implicitHeight: 40

        contentItem: Item {
            FluentIcon {
                anchors.centerIn: parent
                icon: "power"
                implicitSize: 20
            }
        }

        WToolTip {
            extraVisibleCondition: !powerMenu.visible
            text: qsTr("Power")
        }

        onClicked: {
            powerMenu.open()
        }

        WMenu {
            id: powerMenu
            x: -powerMenu.implicitWidth / 2 + powerButton.implicitWidth / 2
            y: -powerMenu.implicitHeight - 4
            Action {
                icon.name: "lock-closed"
                text: "Lock"
                onTriggered: Session.lock()
            }
            Action {
                icon.name: "weather-moon"
                text: "Sleep"
                onTriggered: Session.suspend()
            }
            Action {
                icon.name: "power"
                text: "Shut down"
                onTriggered: Session.poweroff()
            }
            Action {
                icon.name: "arrow-counterclockwise"
                text: "Restart"
                onTriggered: Session.reboot()
            }
        }
    }
}
